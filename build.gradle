/*
 * Copyright (c) 2008-2013 Haulmont. All rights reserved.
 * Use is subject to license terms, see http://www.cuba-platform.com/license for details.
 */

apply plugin: 'java'
apply plugin: 'groovy'
apply plugin: 'maven'
apply plugin: 'idea'

def defaultVersion = '5.6-SNAPSHOT'
def BUILD_VERSION = 'buildVersion'

def artifactVersion = rootProject.hasProperty(BUILD_VERSION) ? rootProject[BUILD_VERSION] : defaultVersion
def isSnapshot = artifactVersion.endsWith('-SNAPSHOT')

group = 'com.haulmont.gradle'
version = artifactVersion

ext.repositoryUrl = 'http://repository.haulmont.com:8587/nexus/content'
ext.repositoryUser = System.getenv('HAULMONT_REPOSITORY_USER')
ext.repositoryPassword = System.getenv('HAULMONT_REPOSITORY_PASSWORD')

repositories {
    mavenLocal()
    maven {
        credentials {
            username repositoryUser
            password repositoryPassword
        }
        url "http://repository.haulmont.com:8587/nexus/content/groups/work"
    }
}

sourceCompatibility = '1.7'
targetCompatibility = '1.7'

configurations {
    deployerJars
}

dependencies {
    compile(group: 'org.apache.openjpa', name: 'openjpa', version: '2.3.0')
    compile(group: 'dom4j', name: 'dom4j', version: '1.6.1')
    compile(group: 'commons-io', name: 'commons-io', version: '1.4')
    compile(group: 'org.codehaus.gpars', name: 'gpars', version: '1.0.0')
    compile(group: 'com.yahoo.platform.yui', name: 'yuicompressor', version: '2.4.6')
    compile(group: 'com.carrotsearch', name: 'smartsprites', version: '0.2.8')
    compile(group: 'com.vaadin', name: 'vaadin-sass-compiler', version: '0.9.12')
    compile(gradleApi())
    compile(localGroovy())
    deployerJars(group: 'org.apache.maven.wagon', name: 'wagon-http', version: '1.0-beta-2')
}

task signVersion() << {
    new File("$buildDir/classes/main/cuba-plugin.version").write("${project.group}:${project.name}:${project.version}")
}

classes.dependsOn signVersion

task sourceJar(type: Jar) {
    from sourceSets.main.groovy
    from sourceSets.main.resources
    classifier = 'sources'
} 

artifacts {
    archives sourceJar
}

def uploadUrl = project.hasProperty('uploadUrl') ? project.uploadUrl :
    "http://repository.haulmont.com:8587/nexus/content/repositories/${isSnapshot ? 'snapshots' : 'releases'}"
def uploadUser = project.hasProperty('uploadUser') ? project.uploadUser :
    System.getenv('HAULMONT_REPOSITORY_USER')
def uploadPassword = project.hasProperty('uploadPassword') ? project.uploadPassword :
    System.getenv('HAULMONT_REPOSITORY_PASSWORD')

uploadArchives.configure {
    repositories.mavenDeployer {
        name = 'httpDeployer'
        configuration = configurations.deployerJars
        repository(url: uploadUrl) {
            authentication(userName: uploadUser, password: uploadPassword)
        }
    }
}
