import groovy.xml.StreamingMarkupBuilder
import groovy.xml.XmlUtil
import org.gradle.util.GradleVersion

/*
 * Copyright (c) 2008-2016 Haulmont.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 *
 */

buildscript {
    repositories {
        mavenLocal()

        if (System.getenv('HAULMONT_REPOSITORY_URL')) {
            maven {
                credentials {
                    username System.getenv('HAULMONT_REPOSITORY_USER')
                    password System.getenv('HAULMONT_REPOSITORY_PASSWORD')
                }
                url System.getenv('HAULMONT_REPOSITORY_URL')
            }
        } else {
            jcenter()
            maven { url "https://dl.bintray.com/cuba-platform/main" }
        }
    }
    dependencies {
        classpath "gradle.plugin.org.jetbrains.gradle.plugin.idea-ext:gradle-idea-ext:0.5"
    }
}

apply plugin: 'java'
apply plugin: 'groovy'
apply plugin: 'maven'
apply plugin: 'org.jetbrains.gradle.plugin.idea-ext'

def defaultVersion = '7.2-SNAPSHOT'
def BUILD_VERSION = 'buildVersion'

def artifactVersion = rootProject.hasProperty(BUILD_VERSION) ? rootProject[BUILD_VERSION] : defaultVersion
def isSnapshot = artifactVersion.endsWith('-SNAPSHOT')

group = 'com.haulmont.gradle'
version = artifactVersion

ext.repositoryUrl = System.getenv('HAULMONT_REPOSITORY_URL') ?: 'https://repo.cuba-platform.com/content/groups/work'
ext.repositoryUser = System.getenv('HAULMONT_REPOSITORY_USER') ?: 'cuba'
ext.repositoryPassword = System.getenv('HAULMONT_REPOSITORY_PASSWORD') ?: 'cuba123'

repositories {
    mavenLocal()
    maven {
        credentials {
            username repositoryUser
            password repositoryPassword
        }
        url repositoryUrl
    }
}

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

configurations {
    deployerJars
    sdk
}

dependencies {
    compile('org.apache.httpcomponents:httpmime:4.5.6')
    compile('org.apache.httpcomponents:httpcore:4.4.10')
    compile('org.javassist:javassist:3.24.1-GA')
    compile('commons-io:commons-io:2.6')
    compile('org.apache.commons:commons-lang3:3.9')

    compile('com.yahoo.platform.yui:yuicompressor:2.4.6')
    compile('com.vaadin:vaadin-sass-compiler:0.9.13') {
        exclude group: 'com.yahoo.platform.yui', module: 'yuicompressor'
    }
    compile('com.jelastic:jelastic-public-j2se:5.0.6')
    compile('com.moowork.gradle:gradle-node-plugin:1.1.0')
    compile('org.apache.commons:commons-dbcp2:2.6.0')
    compile('gradle.plugin.org.jetbrains.gradle.plugin.idea-ext:gradle-idea-ext:0.5')

    compile(gradleApi())
    compile(localGroovy())

    testCompile('junit:junit:4.12')

    deployerJars('org.apache.maven.wagon:wagon-http:3.3.2')

    // Optional dependencies of projects, requried for SDK build

    sdk('com.haulmont.uberjar:uberjar:1.1.0')
    sdk('com.haulmont.frontservlet:frontservlet:1.0.1')
    sdk('org.apache.maven.wagon:wagon-http:3.3.2')
}

def versionProperties = [
        'tomcat.version': '9.0.19',
        'gradle.version': GradleVersion.current().version
]

task sourceJar(type: Jar) {
    from sourceSets.main.groovy
    from sourceSets.main.java
    from sourceSets.main.resources

    classifier = 'sources'
}

def sdkDir = project.file("$project.buildDir/sdk")
def sdkFile = project.file("$sdkDir/META-INF/sdk-pom.xml")

task sdkPom(group: 'build', description: 'Creates POM with optional dependencies of SDK') {
    outputs.file sdkFile
    doLast {
        def sdkDependencies = project.configurations.getByName('sdk').resolvedConfiguration.firstLevelModuleDependencies
        def xml = new StreamingMarkupBuilder()

        def result = xml.bind({ b ->
            mkp.declareNamespace('': 'http://maven.apache.org/POM/4.0.0')
            mkp.declareNamespace(xsi: 'http://www.w3.org/2001/XMLSchema-instance')

            b.'project'('xsi:schemaLocation':
                      'http://maven.apache.org/POM/4.0.0 http://maven.apache.org/xsd/maven-4.0.0.xsd') {

                b.'modelVersion'('4.0.0')
                b.'groupId'('com.haulmont.gradle')
                b.'artifactId'('sdkbom')
                b.'version'(artifactVersion)
                b.'properties' {
                    b.'tomcat.version'(versionProperties['tomcat.version'])
                    b.'gradle.version'(versionProperties['gradle.version'])
                }
                b.'dependencies' {
                    for (d in sdkDependencies) {
                        b.'dependency' {
                            b.'groupId'(d.moduleGroup)
                            b.'artifactId'(d.moduleName)
                            b.'version'(d.moduleVersion)
                        }
                    }
                }
            }
        }) as Writable

        sdkFile.text = XmlUtil.serialize(result)
    }
}

jar.dependsOn(sdkPom)
jar {
    from sdkDir
}

artifacts {
    archives sourceJar
    add('archives', sdkFile) {
        type "sdk"
        builtBy(sdkPom)
    }
}

def uploadUrl = project.hasProperty('uploadUrl') ? project.uploadUrl : null
def haulmontUploadRepo = System.getenv('HAULMONT_REPOSITORY_UPLOAD_URL')
if (uploadUrl == null && haulmontUploadRepo) {
    if (!haulmontUploadRepo.endsWith('/')) {
        haulmontUploadRepo += '/'
    }
    uploadUrl = haulmontUploadRepo + "${isSnapshot ? 'snapshots' : 'releases'}"
}

def uploadUser = project.hasProperty('uploadUser') ? project.uploadUser :
    System.getenv('HAULMONT_REPOSITORY_USER')
def uploadPassword = project.hasProperty('uploadPassword') ? project.uploadPassword :
    System.getenv('HAULMONT_REPOSITORY_PASSWORD')

uploadArchives.configure {
    repositories.mavenDeployer {
        name = 'httpDeployer'
        configuration = configurations.deployerJars
        repository(url: uploadUrl) {
            authentication(userName: uploadUser, password: uploadPassword)
        }
    }
}

if (idea.project) {
    idea.project.settings {
        copyright {
            useDefault = 'Apache'
            profiles {
                Apache {
                    allowReplaceRegexp = ''
                    keyword = 'Copyright'
                    notice = '''Copyright (c) 2008-$today.year Haulmont.

Licensed under the Apache License, Version 2.0 (the "License");
you may not use this file except in compliance with the License.
You may obtain a copy of the License at

    http://www.apache.org/licenses/LICENSE-2.0

Unless required by applicable law or agreed to in writing, software
distributed under the License is distributed on an "AS IS" BASIS,
WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
See the License for the specific language governing permissions and
limitations under the License.'''
                }
            }
        }
    }
}