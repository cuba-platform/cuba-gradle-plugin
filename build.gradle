/*
 * Copyright (c) 2008-2016 Haulmont.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 *
 */

apply plugin: 'java'
apply plugin: 'groovy'
apply plugin: 'maven'
apply plugin: 'idea'

def defaultVersion = '6.3-SNAPSHOT'
def BUILD_VERSION = 'buildVersion'

def artifactVersion = rootProject.hasProperty(BUILD_VERSION) ? rootProject[BUILD_VERSION] : defaultVersion
def isSnapshot = artifactVersion.endsWith('-SNAPSHOT')

group = 'com.haulmont.gradle'
version = artifactVersion

ext.repositoryUrl = System.getenv('HAULMONT_REPOSITORY_URL') ?: 'https://repo.cuba-platform.com/content/groups/work'
ext.repositoryUser = System.getenv('HAULMONT_REPOSITORY_USER') ?: 'cuba'
ext.repositoryPassword = System.getenv('HAULMONT_REPOSITORY_PASSWORD') ?: 'cuba123'

repositories {
    mavenLocal()
    maven {
        credentials {
            username repositoryUser
            password repositoryPassword
        }
        url repositoryUrl
    }
}

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

configurations {
    deployerJars
}

dependencies {
    compile 'com.haulmont.thirdparty:eclipselink:2.6.2.cuba6'
    compile 'org.apache.httpcomponents:httpmime:4.5.1'
    compile 'org.apache.httpcomponents:httpcore:4.4.4'
    compile('org.javassist:javassist:3.20.0-GA')
    compile(group: 'dom4j', name: 'dom4j', version: '1.6.1')
    compile('commons-io:commons-io:2.4')
    compile(group: 'org.codehaus.gpars', name: 'gpars', version: '1.0.0')
    compile(group: 'com.yahoo.platform.yui', name: 'yuicompressor', version: '2.4.6')
    compile(group: 'com.carrotsearch', name: 'smartsprites', version: '0.2.8')
    compile(group: 'com.vaadin', name: 'vaadin-sass-compiler', version: '0.9.13') {
        exclude group: 'com.yahoo.platform.yui', module: 'yuicompressor'
    }
    compile(group: 'com.jelastic', name: 'jelastic-public-j2se', version: '4.0')
    compile(gradleApi())
    compile(localGroovy())
    deployerJars(group: 'org.apache.maven.wagon', name: 'wagon-http', version: '1.0-beta-2')
}

task signVersion() << {
    new File("$buildDir/classes/main/cuba-plugin.version").write("${project.group}:${project.name}:${project.version}")
}

classes.dependsOn signVersion

task sourceJar(type: Jar) {
    from sourceSets.main.groovy
    from sourceSets.main.resources
    classifier = 'sources'
} 

artifacts {
    archives sourceJar
}

def uploadUrl = project.hasProperty('uploadUrl') ? project.uploadUrl : null
def haulmontUploadRepo = System.getenv('HAULMONT_REPOSITORY_UPLOAD_URL')
if (uploadUrl == null && haulmontUploadRepo) {
    if (!haulmontUploadRepo.endsWith('/')) {
        haulmontUploadRepo += '/'
    }
    uploadUrl = haulmontUploadRepo + "${isSnapshot ? 'snapshots' : 'releases'}"
}

def uploadUser = project.hasProperty('uploadUser') ? project.uploadUser :
    System.getenv('HAULMONT_REPOSITORY_USER')
def uploadPassword = project.hasProperty('uploadPassword') ? project.uploadPassword :
    System.getenv('HAULMONT_REPOSITORY_PASSWORD')

uploadArchives.configure {
    repositories.mavenDeployer {
        name = 'httpDeployer'
        configuration = configurations.deployerJars
        repository(url: uploadUrl) {
            authentication(userName: uploadUser, password: uploadPassword)
        }
    }
}

String apacheCopyright = '''Copyright (c) 2008-$today.year Haulmont.

Licensed under the Apache License, Version 2.0 (the "License");
you may not use this file except in compliance with the License.
You may obtain a copy of the License at

    http://www.apache.org/licenses/LICENSE-2.0

Unless required by applicable law or agreed to in writing, software
distributed under the License is distributed on an "AS IS" BASIS,
WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
See the License for the specific language governing permissions and
limitations under the License.'''

if (!hasProperty('isAggregatedProject')) {
    afterEvaluate {
        idea {
            project {
                ipr.withXml { xmlFile ->
                    def copyrightManagerNode = xmlFile.asNode().component.find { it.@name == 'CopyrightManager' }
                    copyrightManagerNode.@default = 'apache'

                    def node = copyrightManagerNode.appendNode('copyright')
                    node.appendNode('option', [name: 'notice', value: apacheCopyright])
                    node.appendNode('option', [name: 'keyword', value: 'Copyright'])
                    node.appendNode('option', [name: 'allowReplaceKeyword', value: ''])
                    node.appendNode('option', [name: 'myName', value: 'apache'])
                    node.appendNode('option', [name: 'myLocal', value: 'true'])
                }
            }
        }
    }
}
