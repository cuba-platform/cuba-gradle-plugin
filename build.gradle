/*
 * Copyright (c) 2008-2016 Haulmont.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 *
 */

apply plugin: 'java'
apply plugin: 'groovy'
apply plugin: 'maven'
apply plugin: 'idea'

def defaultVersion = '7.0-SNAPSHOT'
def BUILD_VERSION = 'buildVersion'

def artifactVersion = rootProject.hasProperty(BUILD_VERSION) ? rootProject[BUILD_VERSION] : defaultVersion
def isSnapshot = artifactVersion.endsWith('-SNAPSHOT')

group = 'com.haulmont.gradle'
version = artifactVersion

ext.repositoryUrl = System.getenv('HAULMONT_REPOSITORY_URL') ?: 'https://repo.cuba-platform.com/content/groups/work'
ext.repositoryUser = System.getenv('HAULMONT_REPOSITORY_USER') ?: 'cuba'
ext.repositoryPassword = System.getenv('HAULMONT_REPOSITORY_PASSWORD') ?: 'cuba123'

repositories {
    mavenLocal()
    maven {
        credentials {
            username repositoryUser
            password repositoryPassword
        }
        url repositoryUrl
    }
}

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

configurations {
    deployerJars
}

dependencies {
    compile('org.apache.httpcomponents:httpmime:4.5.2')
    compile('org.apache.httpcomponents:httpcore:4.4.5')
    compile('org.javassist:javassist:3.22.0-GA')
    compile('commons-io:commons-io:2.4')

    compile('com.yahoo.platform.yui:yuicompressor:2.4.6')
    compile('com.carrotsearch:smartsprites:0.2.11')
    compile('com.vaadin:vaadin-sass-compiler:0.9.13') {
        exclude group: 'com.yahoo.platform.yui', module: 'yuicompressor'
    }
    compile('com.jelastic:jelastic-public-j2se:5.0.6')
    compile('com.moowork.gradle:gradle-node-plugin:1.1.0')
    compile('org.apache.commons:commons-dbcp2:2.1.1')
    compile('gradle.plugin.org.jetbrains.gradle.plugin.idea-ext:gradle-idea-ext:0.2')

    compile(gradleApi())
    compile(localGroovy())

    testCompile('junit:junit:4.12')

    deployerJars('org.apache.maven.wagon:wagon-http:2.12')
}

task sourceJar(type: Jar) {
    from sourceSets.main.groovy
    from sourceSets.main.java
    from sourceSets.main.resources

    classifier = 'sources'
} 

artifacts {
    archives sourceJar
}

def uploadUrl = project.hasProperty('uploadUrl') ? project.uploadUrl : null
def haulmontUploadRepo = System.getenv('HAULMONT_REPOSITORY_UPLOAD_URL')
if (uploadUrl == null && haulmontUploadRepo) {
    if (!haulmontUploadRepo.endsWith('/')) {
        haulmontUploadRepo += '/'
    }
    uploadUrl = haulmontUploadRepo + "${isSnapshot ? 'snapshots' : 'releases'}"
}

def uploadUser = project.hasProperty('uploadUser') ? project.uploadUser :
    System.getenv('HAULMONT_REPOSITORY_USER')
def uploadPassword = project.hasProperty('uploadPassword') ? project.uploadPassword :
    System.getenv('HAULMONT_REPOSITORY_PASSWORD')

uploadArchives.configure {
    repositories.mavenDeployer {
        name = 'httpDeployer'
        configuration = configurations.deployerJars
        repository(url: uploadUrl) {
            authentication(userName: uploadUser, password: uploadPassword)
        }
    }
}

String apacheCopyright = '''Copyright (c) 2008-$today.year Haulmont.

Licensed under the Apache License, Version 2.0 (the "License");
you may not use this file except in compliance with the License.
You may obtain a copy of the License at

    http://www.apache.org/licenses/LICENSE-2.0

Unless required by applicable law or agreed to in writing, software
distributed under the License is distributed on an "AS IS" BASIS,
WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
See the License for the specific language governing permissions and
limitations under the License.'''

if (!hasProperty('isAggregatedProject')) {
    afterEvaluate {
        idea {
            project {
                ipr.withXml { xmlFile ->
                    def copyrightManagerNode = xmlFile.asNode().component.find { it.@name == 'CopyrightManager' }
                    copyrightManagerNode.@default = 'apache'

                    def node = copyrightManagerNode.appendNode('copyright')
                    node.appendNode('option', [name: 'notice', value: apacheCopyright])
                    node.appendNode('option', [name: 'keyword', value: 'Copyright'])
                    node.appendNode('option', [name: 'allowReplaceKeyword', value: ''])
                    node.appendNode('option', [name: 'myName', value: 'apache'])
                    node.appendNode('option', [name: 'myLocal', value: 'true'])
                }
            }
        }
    }
}